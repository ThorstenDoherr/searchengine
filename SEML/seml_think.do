// seml_think.do
// Predicts the false positives in a SearchEngine meta file based on 
// a neural network trained by a sample (see seml_train.do).
//
// Requires:
// meta.txt - full meta export of the result
// seml.brn - neural network generated by seml_train.do
// train.dta - output file from seml_train
// brain package for stata (https://github.com/ThorstenDoherr/brain)
//
// Output:
// think.dta, think.txt - contains searched, found, predicted equal and brain (raw neural network output)
// seml_think.log - log file
//
// Remarks:
// - equal is 1 (match) or 9 (non-match) in think files
// - brain is the "probability" for a match: [0,1]

clear all
set more off
tempfile train
cap log close
log using seml_think.log, replace

brain // shows version number and no of CPUs; not required

// train
use searched found equal using train, clear
replace equal = 9 if equal == 0
sort searched found
save `train'

// meta
use meta, clear

// load brain and input variable names
brain load seml

// thinking
brain think brain
merge 1:1 searched found using `train', keep(master match)
gen byte train = _merge == 3
drop _merge
replace equal = cond(brain > 0.5,1,9)
keep searched found equal brain train
order searched found equal brain train
format brain %8.6f
save think, replace
export delimited think.txt, delim(tab) noquote datafmt replace
log close
